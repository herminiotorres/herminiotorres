<!-- livebook:{"persist_outputs":true} -->

# Plotting with VegaLite

```elixir
Mix.install([
  {:vega_lite, "~> 0.1.6"},
  {:kino_vega_lite, "~> 0.1.10"}
])
```

## Introduction

We need two libraries for plotting in Livebook:

* The [`vega_lite`](https://github.com/elixir-nx/vega_lite)
  package allows us to define our graph specifications

* The [`kino_vega_lite`](https://github.com/livebook-dev/kino_vega_lite) package
  instructs Livebook how to render our specifications

Let's install them by running the setup cell above.

When building graphics we make extensive use of the functions from `VegaLite`,
so it's useful to alias the module as something shorter.

```elixir
alias VegaLite, as: Vl
```

<!-- livebook:{"output":true} -->

```
VegaLite
```

<!-- livebook:{"branch_parent_index":0} -->

## The Chart smart cell

Before we get into exploring all the various chart types, let's have
a look at an awesome feature that comes with `kino_vega_lite` - the
**Chart** smart cell!

Coding up a chart usually involves a couple steps. If you don't know
the API of a particular library, it may be a bit challenging. On the
other hand, if you know the API in-and-out, it's a rather repetitive
task. That's where the **Chart** smart cell comes in, it is a
high-level UI that helps us write our chart code. It is great
a tool for learning and for automating our workflows. Let's
give it a try!

First, we need some data to work with, here's a small excerpt from
the popular [Iris dataset](https://www.kaggle.com/datasets/uciml/iris):

```elixir
iris = [
  %{"petal_length" => 5.1, "petal_width" => 1.9, "species" => "Iris-virginica"},
  %{"petal_length" => 4.0, "petal_width" => 1.3, "species" => "Iris-versicolor"},
  %{"petal_length" => 1.6, "petal_width" => 0.2, "species" => "Iris-setosa"},
  %{"petal_length" => 1.6, "petal_width" => 0.2, "species" => "Iris-setosa"},
  %{"petal_length" => 4.6, "petal_width" => 1.4, "species" => "Iris-versicolor"},
  %{"petal_length" => 4.8, "petal_width" => 1.8, "species" => "Iris-virginica"},
  %{"petal_length" => 5.6, "petal_width" => 2.2, "species" => "Iris-virginica"},
  %{"petal_length" => 5.1, "petal_width" => 1.6, "species" => "Iris-versicolor"},
  %{"petal_length" => 1.5, "petal_width" => 0.3, "species" => "Iris-setosa"},
  %{"petal_length" => 4.5, "petal_width" => 1.6, "species" => "Iris-versicolor"}
]

:ok
```

<!-- livebook:{"output":true} -->

```
:ok
```

Now, to insert a new Chart cell, place your cursor between cells,
click on the <kbd>+ Smart</kbd> button and select Chart.

You can see an example of that below. Click on the "Evaluate"
button to see the chart, then see how it changes as you customize
the parameters.

<!-- livebook:{"attrs":{"chart_title":"Iris","height":200,"layers":[{"active":true,"chart_type":"point","color_field":"species","color_field_aggregate":null,"color_field_bin":null,"color_field_scale_scheme":null,"color_field_type":null,"data_variable":"iris","geodata_color":"blue","latitude_field":null,"longitude_field":null,"x_field":"petal_length","x_field_aggregate":null,"x_field_bin":null,"x_field_scale_type":null,"x_field_type":"quantitative","y_field":"petal_width","y_field_aggregate":null,"y_field_bin":null,"y_field_scale_type":null,"y_field_type":"quantitative"}],"vl_alias":"Elixir.Vl","width":400},"chunks":null,"kind":"Elixir.KinoVegaLite.ChartCell","livebook_object":"smart_cell"} -->

```elixir
Vl.new(width: 400, height: 200, title: "Iris")
|> Vl.data_from_values(iris, only: ["petal_length", "petal_width", "species"])
|> Vl.mark(:point)
|> Vl.encode_field(:x, "petal_length", type: :quantitative)
|> Vl.encode_field(:y, "petal_width", type: :quantitative)
|> Vl.encode_field(:color, "species")
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"values":[{"petal_length":5.1,"petal_width":1.9,"species":"Iris-virginica"},{"petal_length":4.0,"petal_width":1.3,"species":"Iris-versicolor"},{"petal_length":1.6,"petal_width":0.2,"species":"Iris-setosa"},{"petal_length":1.6,"petal_width":0.2,"species":"Iris-setosa"},{"petal_length":4.6,"petal_width":1.4,"species":"Iris-versicolor"},{"petal_length":4.8,"petal_width":1.8,"species":"Iris-virginica"},{"petal_length":5.6,"petal_width":2.2,"species":"Iris-virginica"},{"petal_length":5.1,"petal_width":1.6,"species":"Iris-versicolor"},{"petal_length":1.5,"petal_width":0.3,"species":"Iris-setosa"},{"petal_length":4.5,"petal_width":1.6,"species":"Iris-versicolor"}]},"encoding":{"color":{"field":"species"},"x":{"field":"petal_length","type":"quantitative"},"y":{"field":"petal_width","type":"quantitative"}},"height":200,"mark":"point","title":"Iris","width":400}
```

Under cell actions there is a "Source" button, click on it to
see the source code of your chart. You can even convert it to
a regular Code cell for further adjustments!

The Chart smart cell is one of many Smart cells available
in Livebook ⚡ Not only that, you can create your own Smart
cells too, which we discuss in the
[Exploring Smart cells](/learn/notebooks/smart-cells) notebook!

<!-- livebook:{"branch_parent_index":0} -->

## Basic concepts

Composing a basic Vega-Lite graphic usually consists of the following steps:

```elixir
# Initialize the specification, optionally with some top-level properties
Vl.new(width: 400, height: 400)
# Specify data source for the graphic using one of the data_from_* functions
|> Vl.data_from_values(iteration: 1..100, score: 1..100)
# Pick a visual mark
|> Vl.mark(:line)
# Map data fields to visual properties of the mark, in this case point positions
|> Vl.encode_field(:x, "iteration", type: :quantitative)
|> Vl.encode_field(:y, "score", type: :quantitative)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"values":[{"iteration":1,"score":1},{"iteration":2,"score":2},{"iteration":3,"score":3},{"iteration":4,"score":4},{"iteration":5,"score":5},{"iteration":6,"score":6},{"iteration":7,"score":7},{"iteration":8,"score":8},{"iteration":9,"score":9},{"iteration":10,"score":10},{"iteration":11,"score":11},{"iteration":12,"score":12},{"iteration":13,"score":13},{"iteration":14,"score":14},{"iteration":15,"score":15},{"iteration":16,"score":16},{"iteration":17,"score":17},{"iteration":18,"score":18},{"iteration":19,"score":19},{"iteration":20,"score":20},{"iteration":21,"score":21},{"iteration":22,"score":22},{"iteration":23,"score":23},{"iteration":24,"score":24},{"iteration":25,"score":25},{"iteration":26,"score":26},{"iteration":27,"score":27},{"iteration":28,"score":28},{"iteration":29,"score":29},{"iteration":30,"score":30},{"iteration":31,"score":31},{"iteration":32,"score":32},{"iteration":33,"score":33},{"iteration":34,"score":34},{"iteration":35,"score":35},{"iteration":36,"score":36},{"iteration":37,"score":37},{"iteration":38,"score":38},{"iteration":39,"score":39},{"iteration":40,"score":40},{"iteration":41,"score":41},{"iteration":42,"score":42},{"iteration":43,"score":43},{"iteration":44,"score":44},{"iteration":45,"score":45},{"iteration":46,"score":46},{"iteration":47,"score":47},{"iteration":48,"score":48},{"iteration":49,"score":49},{"iteration":50,"score":50},{"iteration":51,"score":51},{"iteration":52,"score":52},{"iteration":53,"score":53},{"iteration":54,"score":54},{"iteration":55,"score":55},{"iteration":56,"score":56},{"iteration":57,"score":57},{"iteration":58,"score":58},{"iteration":59,"score":59},{"iteration":60,"score":60},{"iteration":61,"score":61},{"iteration":62,"score":62},{"iteration":63,"score":63},{"iteration":64,"score":64},{"iteration":65,"score":65},{"iteration":66,"score":66},{"iteration":67,"score":67},{"iteration":68,"score":68},{"iteration":69,"score":69},{"iteration":70,"score":70},{"iteration":71,"score":71},{"iteration":72,"score":72},{"iteration":73,"score":73},{"iteration":74,"score":74},{"iteration":75,"score":75},{"iteration":76,"score":76},{"iteration":77,"score":77},{"iteration":78,"score":78},{"iteration":79,"score":79},{"iteration":80,"score":80},{"iteration":81,"score":81},{"iteration":82,"score":82},{"iteration":83,"score":83},{"iteration":84,"score":84},{"iteration":85,"score":85},{"iteration":86,"score":86},{"iteration":87,"score":87},{"iteration":88,"score":88},{"iteration":89,"score":89},{"iteration":90,"score":90},{"iteration":91,"score":91},{"iteration":92,"score":92},{"iteration":93,"score":93},{"iteration":94,"score":94},{"iteration":95,"score":95},{"iteration":96,"score":96},{"iteration":97,"score":97},{"iteration":98,"score":98},{"iteration":99,"score":99},{"iteration":100,"score":100}]},"encoding":{"x":{"field":"iteration","type":"quantitative"},"y":{"field":"score","type":"quantitative"}},"height":400,"mark":"line","width":400}
```

Below you can find a number of example graphics for common use cases.
For a number of plain Vega-Lite examples you can look
[here](https://vega.github.io/vega-lite/examples).

<!-- livebook:{"branch_parent_index":0} -->

## Bar charts

### Simple bar chart

A bar chart encodes quantitative values as the length of regular bars.

```elixir
# Source: https://vega.github.io/vega-lite/examples/bar.html

data = [
  %{"a" => "A", "b" => 28},
  %{"a" => "B", "b" => 55},
  %{"a" => "C", "b" => 43},
  %{"a" => "D", "b" => 91},
  %{"a" => "E", "b" => 81},
  %{"a" => "F", "b" => 53},
  %{"a" => "G", "b" => 19},
  %{"a" => "H", "b" => 87},
  %{"a" => "I", "b" => 52}
]

Vl.new(width: 400, height: 300)
|> Vl.data_from_values(data)
|> Vl.mark(:bar)
|> Vl.encode_field(:x, "a", type: :nominal, axis: [label_angle: 0])
|> Vl.encode_field(:y, "b", type: :quantitative)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"values":[{"a":"A","b":28},{"a":"B","b":55},{"a":"C","b":43},{"a":"D","b":91},{"a":"E","b":81},{"a":"F","b":53},{"a":"G","b":19},{"a":"H","b":87},{"a":"I","b":52}]},"encoding":{"x":{"axis":{"labelAngle":0},"field":"a","type":"nominal"},"y":{"field":"b","type":"quantitative"}},"height":300,"mark":"bar","width":400}
```

### Stacked bar chart

A stacked bar chart contains multi-color bars to represent
several quantitative values at once.

```elixir
# Source: https://vega.github.io/vega-lite/examples/stacked_bar_weather.html

Vl.new(width: 300, height: 200)
|> Vl.data_from_url("https://vega.github.io/editor/data/seattle-weather.csv")
|> Vl.mark(:bar)
|> Vl.encode_field(:x, "date", time_unit: :month, type: :ordinal, title: "Month of the year")
|> Vl.encode(:y, aggregate: :count, type: :quantitative, title: "Number of days")
|> Vl.encode_field(:color, "weather",
  type: :nominal,
  title: "Weather type",
  scale: [
    domain: ["sun", "fog", "drizzle", "rain", "snow"],
    range: ["#e7ba52", "#c7c7c7", "#aec7e8", "#1f77b4", "#9467bd"]
  ]
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/seattle-weather.csv"},"encoding":{"color":{"field":"weather","scale":{"domain":["sun","fog","drizzle","rain","snow"],"range":["#e7ba52","#c7c7c7","#aec7e8","#1f77b4","#9467bd"]},"title":"Weather type","type":"nominal"},"x":{"field":"date","timeUnit":"month","title":"Month of the year","type":"ordinal"},"y":{"aggregate":"count","title":"Number of days","type":"quantitative"}},"height":200,"mark":"bar","width":300}
```

### Grouped bar chart

Graphing one bar plot per group.

```elixir
# Source: https://vega.github.io/vega-lite/examples/bar_grouped.html

Vl.new(width: [step: 12])
|> Vl.data_from_url("https://vega.github.io/editor/data/population.json")
|> Vl.transform(filter: "datum.year == 2000")
|> Vl.transform(calculate: "datum.sex == 2 ? 'Female' : 'Male'", as: "gender")
|> Vl.mark(:bar)
|> Vl.encode_field(:column, "age", type: :ordinal, spacing: 10)
|> Vl.encode_field(:y, "people", aggregate: :sum, title: "population", axis: [grid: false])
|> Vl.encode_field(:x, "gender", title: nil)
|> Vl.encode_field(:color, "gender")
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"url":"https://vega.github.io/editor/data/population.json"},"encoding":{"color":{"field":"gender"},"column":{"field":"age","spacing":10,"type":"ordinal"},"x":{"field":"gender","title":null},"y":{"aggregate":"sum","axis":{"grid":false},"field":"people","title":"population"}},"mark":"bar","transform":[{"filter":"datum.year == 2000"},{"as":"gender","calculate":"datum.sex == 2 ? 'Female' : 'Male'"}],"width":{"step":12}}
```

<!-- livebook:{"branch_parent_index":0} -->

## Histograms, density plots and dot plots

### Histogram

A histogram represents the value frequency in predefined intervals.

```elixir
# Source: https://vega.github.io/vega-lite/examples/histogram.html

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/movies.json")
|> Vl.mark(:bar)
|> Vl.encode_field(:x, "IMDB Rating", bin: true)
|> Vl.encode(:y, aggregate: :count)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/movies.json"},"encoding":{"x":{"bin":true,"field":"IMDB Rating"},"y":{"aggregate":"count"}},"mark":"bar"}
```

### Density plot

A density plot represents the distribution estimate of a numeric value.

```elixir
# Source: https://vega.github.io/vega-lite/examples/area_density.html

Vl.new(width: 400, height: 100)
|> Vl.data_from_url("https://vega.github.io/editor/data/movies.json")
|> Vl.transform(density: "IMDB Rating")
|> Vl.mark(:area)
|> Vl.encode_field(:x, "value", type: :quantitative, title: "IMDB rating")
|> Vl.encode_field(:y, "density", type: :quantitative)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/movies.json"},"encoding":{"x":{"field":"value","title":"IMDB rating","type":"quantitative"},"y":{"field":"density","type":"quantitative"}},"height":100,"mark":"area","transform":[{"density":"IMDB Rating"}],"width":400}
```

### Stacked density estimates

Several density plots stacked together.

```elixir
# Source: https://vega.github.io/vega-lite/examples/area_density_stacked.html

Vl.new(width: 400, height: 80)
|> Vl.data_from_url("https://vega.github.io/editor/data/penguins.json")
|> Vl.transform(density: "Body Mass (g)", groupby: ["Species"], extent: [2500, 6500])
|> Vl.mark(:area)
|> Vl.encode_field(:x, "value", type: :quantitative, title: "Body mass (g)")
|> Vl.encode_field(:y, "density", type: :quantitative, stack: true)
|> Vl.encode_field(:color, "Species", type: :nominal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/penguins.json"},"encoding":{"color":{"field":"Species","type":"nominal"},"x":{"field":"value","title":"Body mass (g)","type":"quantitative"},"y":{"field":"density","stack":true,"type":"quantitative"}},"height":80,"mark":"area","transform":[{"density":"Body Mass (g)","extent":[2500,6500],"groupby":["Species"]}],"width":400}
```

### 2D Histogram scatterplot

A 2D version of a regular histogram, with intervals in both axis
and frequency represented by point size.

```elixir
# Source: https://vega.github.io/vega-lite/examples/circle_binned.html

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/movies.json")
|> Vl.mark(:circle)
|> Vl.encode_field(:x, "IMDB Rating", bin: [maxbins: 10])
|> Vl.encode_field(:y, "Rotten Tomatoes Rating", bin: [maxbins: 10])
|> Vl.encode(:size, aggregate: :count)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/movies.json"},"encoding":{"size":{"aggregate":"count"},"x":{"bin":{"maxbins":10},"field":"IMDB Rating"},"y":{"bin":{"maxbins":10},"field":"Rotten Tomatoes Rating"}},"mark":"circle"}
```

### 2D Histogram heatmap

Another version of 2D histogram, with color scale representing value frequency.

```elixir
# Source: https://vega.github.io/vega-lite/examples/rect_binned_heatmap.html

Vl.new(width: 300, height: 200)
|> Vl.data_from_url("https://vega.github.io/editor/data/movies.json")
|> Vl.transform(
  filter: [
    and: [
      [field: "IMDB Rating", valid: true],
      [field: "Rotten Tomatoes Rating", valid: true]
    ]
  ]
)
|> Vl.mark(:rect)
|> Vl.encode_field(:x, "IMDB Rating", bin: [maxbins: 60])
|> Vl.encode_field(:y, "Rotten Tomatoes Rating", bin: [maxbins: 40])
|> Vl.encode(:color, aggregate: :count)
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"url":"https://vega.github.io/editor/data/movies.json"},"encoding":{"color":{"aggregate":"count"},"x":{"bin":{"maxbins":60},"field":"IMDB Rating"},"y":{"bin":{"maxbins":40},"field":"Rotten Tomatoes Rating"}},"height":200,"mark":"rect","transform":[{"filter":{"and":[{"field":"IMDB Rating","valid":true},{"field":"Rotten Tomatoes Rating","valid":true}]}}],"width":300}
```

### 2D Ordinal heatmap

A heatmap similar to the above, but with already discrete categories.

```elixir
# Source: https://vega.github.io/vega-lite/examples/rect_heatmap_weather.html

Vl.new(title: "Daily max temperatures (C) in Seattle, WA")
|> Vl.data_from_url("https://vega.github.io/editor/data/seattle-weather.csv")
|> Vl.mark(:rect)
|> Vl.encode_field(:x, "date",
  time_unit: :date,
  type: :ordinal,
  title: "Day",
  axis: [label_angle: 0, format: "%e"]
)
|> Vl.encode_field(:y, "date",
  time_unit: :month,
  type: :ordinal,
  title: "Month"
)
|> Vl.encode_field(:color, "temp_max",
  aggregate: :max,
  type: :quantitative,
  legend: [title: nil]
)
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"url":"https://vega.github.io/editor/data/seattle-weather.csv"},"encoding":{"color":{"aggregate":"max","field":"temp_max","legend":{"title":null},"type":"quantitative"},"x":{"axis":{"format":"%e","labelAngle":0},"field":"date","timeUnit":"date","title":"Day","type":"ordinal"},"y":{"field":"date","timeUnit":"month","title":"Month","type":"ordinal"}},"mark":"rect","title":"Daily max temperatures (C) in Seattle, WA"}
```

<!-- livebook:{"branch_parent_index":0} -->

## Scatter and strip plots

### Scatterplot

A scatterplot represents 2D data directly as geometric points.

```elixir
# Source: https://vega.github.io/vega-lite/examples/point_2d.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/cars.json")
|> Vl.mark(:point)
|> Vl.encode_field(:x, "Horsepower", type: :quantitative)
|> Vl.encode_field(:y, "Miles_per_Gallon", type: :quantitative)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/cars.json"},"encoding":{"x":{"field":"Horsepower","type":"quantitative"},"y":{"field":"Miles_per_Gallon","type":"quantitative"}},"height":300,"mark":"point","width":400}
```

### Strip plot

Shows the relationship between two values using tick marks.

```elixir
# Source: https://vega.github.io/vega-lite/examples/tick_strip.html

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/cars.json")
|> Vl.mark(:tick)
|> Vl.encode_field(:x, "Horsepower", type: :quantitative)
|> Vl.encode_field(:y, "Cylinders", type: :ordinal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/cars.json"},"encoding":{"x":{"field":"Horsepower","type":"quantitative"},"y":{"field":"Cylinders","type":"ordinal"}},"mark":"tick"}
```

### Colored scatterplot

Scatterplot with clear point groups.

```elixir
# Source: https://vega.github.io/vega-lite/examples/point_color_with_shape.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/penguins.json")
|> Vl.mark(:point)
|> Vl.encode_field(:x, "Flipper Length (mm)", type: :quantitative, scale: [zero: false])
|> Vl.encode_field(:y, "Body Mass (g)", type: :quantitative, scale: [zero: false])
|> Vl.encode_field(:color, "Species", type: :nominal)
|> Vl.encode_field(:shape, "Species", type: :nominal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/penguins.json"},"encoding":{"color":{"field":"Species","type":"nominal"},"shape":{"field":"Species","type":"nominal"},"x":{"field":"Flipper Length (mm)","scale":{"zero":false},"type":"quantitative"},"y":{"field":"Body Mass (g)","scale":{"zero":false},"type":"quantitative"}},"height":300,"mark":"point","width":400}
```

<!-- livebook:{"branch_parent_index":0} -->

## Line charts

### Line chart

A simple chart resulting from linking individual points.

```elixir
# Source: https://vega.github.io/vega-lite/examples/line.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/stocks.csv")
|> Vl.transform(filter: "datum.symbol == 'GOOG'")
|> Vl.mark(:line)
|> Vl.encode_field(:x, "date", type: :temporal)
|> Vl.encode_field(:y, "price", type: :quantitative)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/stocks.csv"},"encoding":{"x":{"field":"date","type":"temporal"},"y":{"field":"price","type":"quantitative"}},"height":300,"mark":"line","transform":[{"filter":"datum.symbol == 'GOOG'"}],"width":400}
```

### Multi series line chart

Multiple line charts combined together.

```elixir
# Source: https://vega.github.io/vega-lite/examples/line_color.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/stocks.csv")
|> Vl.mark(:line)
|> Vl.encode_field(:x, "date", type: :temporal)
|> Vl.encode_field(:y, "price", type: :quantitative)
|> Vl.encode_field(:color, "symbol", type: :nominal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/stocks.csv"},"encoding":{"color":{"field":"symbol","type":"nominal"},"x":{"field":"date","type":"temporal"},"y":{"field":"price","type":"quantitative"}},"height":300,"mark":"line","width":400}
```

### Line chart with point markers

Marking individual points on top of the line.

```elixir
# Source: https://vega.github.io/vega-lite/examples/line_color.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/stocks.csv")
|> Vl.mark(:line, point: true)
|> Vl.encode_field(:x, "date", time_unit: :year, type: :temporal)
|> Vl.encode_field(:y, "price", aggregate: :mean, type: :quantitative)
|> Vl.encode_field(:color, "symbol", type: :nominal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/stocks.csv"},"encoding":{"color":{"field":"symbol","type":"nominal"},"x":{"field":"date","timeUnit":"year","type":"temporal"},"y":{"aggregate":"mean","field":"price","type":"quantitative"}},"height":300,"mark":{"point":true,"type":"line"},"width":400}
```

### Sequence generators

Line charts using generated data.

```elixir
# Source: https://vega.github.io/vega-lite/examples/sequence_line_fold.html

Vl.new(width: 300, height: 150)
|> Vl.data(sequence: [start: 0, stop: 12.7, step: 0.1, as: "x"])
|> Vl.transform(calculate: "sin(datum.x)", as: "sin(x)")
|> Vl.transform(calculate: "cos(datum.x)", as: "cos(x)")
|> Vl.transform(fold: ["sin(x)", "cos(x)"])
|> Vl.mark(:line)
|> Vl.encode_field(:x, "x", type: :quantitative)
|> Vl.encode_field(:y, "value", type: :quantitative)
|> Vl.encode_field(:color, "key", type: :nominal, title: nil)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"sequence":{"as":"x","start":0,"step":0.1,"stop":12.7}},"encoding":{"color":{"field":"key","title":null,"type":"nominal"},"x":{"field":"x","type":"quantitative"},"y":{"field":"value","type":"quantitative"}},"height":150,"mark":"line","transform":[{"as":"sin(x)","calculate":"sin(datum.x)"},{"as":"cos(x)","calculate":"cos(datum.x)"},{"fold":["sin(x)","cos(x)"]}],"width":300}
```

<!-- livebook:{"branch_parent_index":0} -->

## Area charts and streamgraphs

### Area chart

An area chart represents quantitative data and is based on line chart.

```elixir
# Source: https://vega.github.io/vega-lite/examples/area.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/unemployment-across-industries.json")
|> Vl.mark(:area)
|> Vl.encode_field(:x, "date", time_unit: :yearmonth, axis: [format: "%Y"])
|> Vl.encode_field(:y, "count", aggregate: :sum, title: "count")
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/unemployment-across-industries.json"},"encoding":{"x":{"axis":{"format":"%Y"},"field":"date","timeUnit":"yearmonth"},"y":{"aggregate":"sum","field":"count","title":"count"}},"height":300,"mark":"area","width":400}
```

### Stacked area chart

A combination of multiple area charts allowing for easy visual comparison.

```elixir
# Source: https://vega.github.io/vega-lite/examples/stacked_area.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/unemployment-across-industries.json")
|> Vl.mark(:area)
|> Vl.encode_field(:x, "date", time_unit: :yearmonth, axis: [format: "%Y"])
|> Vl.encode_field(:y, "count", aggregate: :sum, title: "count")
|> Vl.encode_field(:color, "series", scale: [scheme: "category20b"])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/unemployment-across-industries.json"},"encoding":{"color":{"field":"series","scale":{"scheme":"category20b"}},"x":{"axis":{"format":"%Y"},"field":"date","timeUnit":"yearmonth"},"y":{"aggregate":"sum","field":"count","title":"count"}},"height":300,"mark":"area","width":400}
```

### Streamgraph

A streamgraph is a type of area chart which is displaced around a central axis.

```elixir
Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/unemployment-across-industries.json")
|> Vl.mark(:area)
|> Vl.encode_field(:x, "date", time_unit: :yearmonth, axis: [format: "%Y"])
|> Vl.encode_field(:y, "count", aggregate: :sum, axis: nil, stack: :center)
|> Vl.encode_field(:color, "series", scale: [scheme: "category20b"])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/unemployment-across-industries.json"},"encoding":{"color":{"field":"series","scale":{"scheme":"category20b"}},"x":{"axis":{"format":"%Y"},"field":"date","timeUnit":"yearmonth"},"y":{"aggregate":"sum","axis":null,"field":"count","stack":"center"}},"height":300,"mark":"area","width":400}
```

<!-- livebook:{"branch_parent_index":0} -->

## Circular plots

### Pie chart

A pie chart encodes proportional differences among a set of numeric values
as the angular extent and area of a circular slice.

```elixir
data = [
  %{"category" => 1, "value" => 4},
  %{"category" => 2, "value" => 6},
  %{"category" => 3, "value" => 10},
  %{"category" => 4, "value" => 3},
  %{"category" => 5, "value" => 7},
  %{"category" => 6, "value" => 8}
]

Vl.new()
|> Vl.data_from_values(data)
|> Vl.mark(:arc)
|> Vl.encode_field(:theta, "value", type: :quantitative)
|> Vl.encode_field(:color, "category", type: :nominal)
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"values":[{"category":1,"value":4},{"category":2,"value":6},{"category":3,"value":10},{"category":4,"value":3},{"category":5,"value":7},{"category":6,"value":8}]},"encoding":{"color":{"field":"category","type":"nominal"},"theta":{"field":"value","type":"quantitative"}},"mark":"arc"}
```

### Radial plot

This radial plot uses both angular and radial extent to convey
multiple dimensions of data. However, this approach is not perceptually
effective, as viewers will most likely be drawn to the total area of
the shape, conflating the two dimensions.

```elixir
# Source: https://vega.github.io/vega-lite/examples/arc_radial.html

Vl.new()
|> Vl.data_from_values(data: [12, 23, 47, 6, 52, 19])
|> Vl.encode_field(:theta, "data", type: :quantitative, stack: true)
|> Vl.encode_field(:radius, "data", scale: [type: :sqrt, zero: true, range_min: 20])
|> Vl.encode_field(:color, "data", type: :nominal, legend: nil)
|> Vl.layers([
  Vl.new()
  |> Vl.mark(:arc, inner_radius: 20, stroke: "#fff"),
  Vl.new()
  |> Vl.mark(:text, radius_offset: 10)
  |> Vl.encode_field(:text, "data", type: :quantitative)
])
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"values":[{"data":12},{"data":23},{"data":47},{"data":6},{"data":52},{"data":19}]},"encoding":{"color":{"field":"data","legend":null,"type":"nominal"},"radius":{"field":"data","scale":{"rangeMin":20,"type":"sqrt","zero":true}},"theta":{"field":"data","stack":true,"type":"quantitative"}},"layer":[{"mark":{"innerRadius":20,"stroke":"#fff","type":"arc"}},{"encoding":{"text":{"field":"data","type":"quantitative"}},"mark":{"radiusOffset":10,"type":"text"}}]}
```

<!-- livebook:{"branch_parent_index":0} -->

## Calculations

### Layering rolling averages over raw values

Raw value points and a calculated rolling average on top.

```elixir
# Source: https://vega.github.io/vega-lite/examples/layer_line_rolling_mean_point_raw.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/seattle-weather.csv")
|> Vl.transform(
  window: [
    [field: "temp_max", op: :mean, as: "rolling_mean"]
  ],
  frame: [-15, 15]
)
|> Vl.encode_field(:x, "date", type: :temporal, title: "Date", opacity: 0.3)
|> Vl.layers([
  Vl.new()
  |> Vl.mark(:point, opacity: 0.3)
  |> Vl.encode_field(:y, "temp_max", type: :quantitative),
  Vl.new()
  |> Vl.mark(:line, color: :red, size: 3)
  |> Vl.encode_field(:y, "rolling_mean",
    type: :quantitative,
    title: "Rolling mean of max temperature"
  )
])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/seattle-weather.csv"},"encoding":{"x":{"field":"date","opacity":0.3,"title":"Date","type":"temporal"}},"height":300,"layer":[{"encoding":{"y":{"field":"temp_max","type":"quantitative"}},"mark":{"opacity":0.3,"type":"point"}},{"encoding":{"y":{"field":"rolling_mean","title":"Rolling mean of max temperature","type":"quantitative"}},"mark":{"color":"red","size":3,"type":"line"}}],"transform":[{"frame":[-15,15],"window":[{"as":"rolling_mean","field":"temp_max","op":"mean"}]}],"width":400}
```

### Linear regression

Linear regression is an approach of finding a line that best
represents the relationship in the data.

```elixir
# Source: https://vega.github.io/vega-lite/examples/layer_point_line_regression.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/movies.json")
|> Vl.layers([
  Vl.new()
  |> Vl.mark(:point, filled: true)
  |> Vl.encode_field(:x, "Rotten Tomatoes Rating", type: :quantitative)
  |> Vl.encode_field(:y, "IMDB Rating", type: :quantitative),
  Vl.new()
  |> Vl.mark(:line, color: :firebrick)
  |> Vl.transform(regression: "IMDB Rating", on: "Rotten Tomatoes Rating")
  |> Vl.encode_field(:x, "Rotten Tomatoes Rating", type: :quantitative)
  |> Vl.encode_field(:y, "IMDB Rating", type: :quantitative)
])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/movies.json"},"height":300,"layer":[{"encoding":{"x":{"field":"Rotten Tomatoes Rating","type":"quantitative"},"y":{"field":"IMDB Rating","type":"quantitative"}},"mark":{"filled":true,"type":"point"}},{"encoding":{"x":{"field":"Rotten Tomatoes Rating","type":"quantitative"},"y":{"field":"IMDB Rating","type":"quantitative"}},"mark":{"color":"firebrick","type":"line"},"transform":[{"on":"Rotten Tomatoes Rating","regression":"IMDB Rating"}]}],"width":400}
```

<!-- livebook:{"branch_parent_index":0} -->

## Composite marks

### Error bars showing standard deviation

Adding observations standard deviation alongside the mean point.

```elixir
# Source: https://vega.github.io/vega-lite/examples/layer_point_errorbar_ci.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/barley.json")
|> Vl.encode_field(:y, "variety", type: :ordinal)
|> Vl.layers([
  Vl.new()
  |> Vl.mark(:point, filled: true)
  |> Vl.encode_field(:x, "yield",
    aggregate: :mean,
    type: :quantitative,
    scale: [zero: false],
    title: "Barley yield"
  )
  |> Vl.encode(:color, value: :black),
  Vl.new()
  |> Vl.mark(:errorbar, extent: :stdev)
  |> Vl.encode_field(:x, "yield", type: :quantitative, title: "Barley yield")
])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/barley.json"},"encoding":{"y":{"field":"variety","type":"ordinal"}},"height":300,"layer":[{"encoding":{"color":{"value":"black"},"x":{"aggregate":"mean","field":"yield","scale":{"zero":false},"title":"Barley yield","type":"quantitative"}},"mark":{"filled":true,"type":"point"}},{"encoding":{"x":{"field":"yield","title":"Barley yield","type":"quantitative"}},"mark":{"extent":"stdev","type":"errorbar"}}],"width":400}
```

### Line chart with confidence interval band

Line with confidence band, which represents the uncertainty of an estimate function.

```elixir
# Source: https://vega.github.io/vega-lite/examples/layer_line_errorband_ci.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/cars.json")
|> Vl.encode_field(:x, "Year", time_unit: :year)
|> Vl.layers([
  Vl.new()
  |> Vl.mark(:errorband, extent: :ci)
  |> Vl.encode_field(:y, "Miles_per_Gallon",
    type: :quantitative,
    title: "Mean of miles per gallon (95% CIs)"
  ),
  Vl.new()
  |> Vl.mark(:line)
  |> Vl.encode_field(:y, "Miles_per_Gallon", aggregate: :mean)
])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/cars.json"},"encoding":{"x":{"field":"Year","timeUnit":"year"}},"height":300,"layer":[{"encoding":{"y":{"field":"Miles_per_Gallon","title":"Mean of miles per gallon (95% CIs)","type":"quantitative"}},"mark":{"extent":"ci","type":"errorband"}},{"encoding":{"y":{"aggregate":"mean","field":"Miles_per_Gallon"}},"mark":"line"}],"width":400}
```

<!-- livebook:{"branch_parent_index":0} -->

## Box plots

### Box plot with min/max whiskers

A vertical box plot showing median, min, and max values.

```elixir
# Source: https://vega.github.io/vega-lite/examples/boxplot_minmax_2D_vertical.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/penguins.json")
|> Vl.mark(:boxplot, extent: "min-max")
|> Vl.encode_field(:x, "Species", type: :nominal)
|> Vl.encode_field(:color, "Species", type: :nominal, legend: nil)
|> Vl.encode_field(:y, "Body Mass (g)", type: :quantitative, scale: [zero: false])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/penguins.json"},"encoding":{"color":{"field":"Species","legend":null,"type":"nominal"},"x":{"field":"Species","type":"nominal"},"y":{"field":"Body Mass (g)","scale":{"zero":false},"type":"quantitative"}},"height":300,"mark":{"extent":"min-max","type":"boxplot"},"width":400}
```

<!-- livebook:{"branch_parent_index":0} -->

## Faceting

### Trellis bar chart

Trellis display is a series of graphs with the same scale and axes
split according to some criteria, so that they are easy to compare.

```elixir
# Source: https://vega.github.io/vega-lite/examples/trellis_bar.html

Vl.new(width: [step: 17])
|> Vl.data_from_url("https://vega.github.io/editor/data/population.json")
|> Vl.transform(filter: "datum.year == 2000")
|> Vl.transform(calculate: "datum.sex == 2 ? 'Female' : 'Male'", as: "gender")
|> Vl.mark(:bar)
|> Vl.encode_field(:row, "gender")
|> Vl.encode_field(:x, "age")
|> Vl.encode_field(:y, "people", aggregate: :sum, title: "population")
|> Vl.encode_field(:color, "gender")
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/population.json"},"encoding":{"color":{"field":"gender"},"row":{"field":"gender"},"x":{"field":"age"},"y":{"aggregate":"sum","field":"people","title":"population"}},"mark":"bar","transform":[{"filter":"datum.year == 2000"},{"as":"gender","calculate":"datum.sex == 2 ? 'Female' : 'Male'"}],"width":{"step":17}}
```

### Trellis area chart

Similar to the above, except for area charts.

```elixir
# Source: https://vega.github.io/vega-lite/examples/trellis_area.html

Vl.new(width: 400, height: 60)
|> Vl.data_from_url("https://vega.github.io/editor/data/stocks.csv")
|> Vl.transform(filter: "datum.symbol !== 'GOOG'")
|> Vl.mark(:area)
|> Vl.encode_field(:x, "date", type: :temporal, axis: [grid: false])
|> Vl.encode_field(:y, "price", type: :quantitative, axis: [grid: false])
|> Vl.encode_field(:color, "symbol", type: :nominal, legend: nil)
|> Vl.encode_field(:row, "symbol", type: :nominal, title: "Symbol")
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/stocks.csv"},"encoding":{"color":{"field":"symbol","legend":null,"type":"nominal"},"row":{"field":"symbol","title":"Symbol","type":"nominal"},"x":{"axis":{"grid":false},"field":"date","type":"temporal"},"y":{"axis":{"grid":false},"field":"price","type":"quantitative"}},"height":60,"mark":"area","transform":[{"filter":"datum.symbol !== 'GOOG'"}],"width":400}
```

### Trellis multi-level scatterplot

Again, the trellis display, but this time for scatterplot
and grouped data.

```elixir
# Source: https://vega.github.io/vega-lite/examples/trellis_barley.html

Vl.new(name: "Trellis Barley", height: [step: 12])
|> Vl.data_from_url("https://vega.github.io/editor/data/barley.json")
|> Vl.mark(:point)
|> Vl.encode_field(:facet, "site",
  type: :ordinal,
  columns: 2,
  sort: [op: :median, field: "yield"]
)
|> Vl.encode_field(:x, "yield", aggregate: :median, type: :quantitative, scale: [zero: false])
|> Vl.encode_field(:y, "variety", type: :ordinal, sort: "-x")
|> Vl.encode_field(:color, "year", type: :nominal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/barley.json"},"encoding":{"color":{"field":"year","type":"nominal"},"facet":{"columns":2,"field":"site","sort":{"field":"yield","op":"median"},"type":"ordinal"},"x":{"aggregate":"median","field":"yield","scale":{"zero":false},"type":"quantitative"},"y":{"field":"variety","sort":"-x","type":"ordinal"}},"height":{"step":12},"mark":"point","name":"Trellis Barley"}
```

<!-- livebook:{"branch_parent_index":0} -->

## Repeated graphics

### Repeated layer

A multi-layer chart composed by repeating the same specification over several fields.

```elixir
# Source: https://vega.github.io/vega-lite/examples/repeat_layer.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/movies.json")
|> Vl.repeat(
  [layer: ["US Gross", "Worldwide Gross"]],
  Vl.new()
  |> Vl.mark(:line)
  |> Vl.encode_field(:x, "IMDB Rating", bin: true, type: :quantitative)
  |> Vl.encode_repeat(:y, :layer,
    aggregate: :mean,
    type: :quantitative,
    title: "Mean of US and Worldwide Gross"
  )
  |> Vl.encode(:color, datum: [repeat: :layer], type: :nominal)
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/movies.json"},"height":300,"repeat":{"layer":["US Gross","Worldwide Gross"]},"spec":{"encoding":{"color":{"datum":{"repeat":"layer"},"type":"nominal"},"x":{"bin":true,"field":"IMDB Rating","type":"quantitative"},"y":{"aggregate":"mean","field":{"repeat":"layer"},"title":"Mean of US and Worldwide Gross","type":"quantitative"}},"mark":"line"},"width":400}
```

### Repeated figure

A multi-view chart composed by repeating the same specification over several fields.

```elixir
# Source: https://vega.github.io/vega-lite/docs/repeat.html#repeated-line-charts

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/weather.csv")
|> Vl.repeat(
  ["temp_max", "precipitation", "wind"],
  Vl.new()
  |> Vl.mark(:line)
  |> Vl.encode_field(:x, "date", time_unit: :month)
  |> Vl.encode_repeat(:y, :repeat, aggregate: :mean)
  |> Vl.encode_field(:color, "location")
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/weather.csv"},"repeat":["temp_max","precipitation","wind"],"spec":{"encoding":{"color":{"field":"location"},"x":{"field":"date","timeUnit":"month"},"y":{"aggregate":"mean","field":{"repeat":"repeat"}}},"mark":"line"}}
```

### Scatterplot Matrix (SPLOM)

Scatterplot matrix (SPLOM) is a series of graphics for different
pairs of variables, it's useful to determine possible correlation
between some variables.

```elixir
# Source: https://vega.github.io/vega-lite/docs/repeat.html#scatterplot-matrix-splom

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/penguins.json")
|> Vl.repeat(
  [
    row: [
      "Beak Length (mm)",
      "Beak Depth (mm)",
      "Flipper Length (mm)",
      "Body Mass (g)"
    ],
    column: [
      "Body Mass (g)",
      "Flipper Length (mm)",
      "Beak Depth (mm)",
      "Beak Length (mm)"
    ]
  ],
  Vl.new(width: 150, height: 150)
  |> Vl.mark(:point)
  |> Vl.encode_repeat(:x, :column, type: :quantitative, scale: [zero: false])
  |> Vl.encode_repeat(:y, :row, type: :quantitative, scale: [zero: false])
  |> Vl.encode_field(:color, "Species", type: :nominal)
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/penguins.json"},"repeat":{"column":["Body Mass (g)","Flipper Length (mm)","Beak Depth (mm)","Beak Length (mm)"],"row":["Beak Length (mm)","Beak Depth (mm)","Flipper Length (mm)","Body Mass (g)"]},"spec":{"encoding":{"color":{"field":"Species","type":"nominal"},"x":{"field":{"repeat":"column"},"scale":{"zero":false},"type":"quantitative"},"y":{"field":{"repeat":"row"},"scale":{"zero":false},"type":"quantitative"}},"height":150,"mark":"point","width":150}}
```

<!-- livebook:{"branch_parent_index":0} -->

## Layering

### Layered charts with separate scales

Layered charts may concern variables of different units and scales,
in which case we can display the scales separately.

```elixir
# Source: https://vega.github.io/vega-lite/docs/layer.html#combined-scales-and-guides

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/weather.csv")
|> Vl.transform(filter: "datum.location == 'Seattle'")
|> Vl.encode_field(:x, "date", time_unit: :month, axis: [format: "%b", title: nil])
|> Vl.layers([
  Vl.new()
  |> Vl.mark(:area, opacity: 0.3, color: "#85C5A6")
  |> Vl.encode_field(:y, "temp_max",
    aggregate: :average,
    scale: [domain: [0, 30]],
    title: "Avg. Temperature (°C)",
    axis: [title_color: "#85C5A6"]
  )
  |> Vl.encode_field(:y2, "temp_min", aggregate: :average),
  Vl.new()
  |> Vl.mark(:line, interpolate: :monotone, stroke: "#85A9C5")
  |> Vl.encode_field(:y, "precipitation",
    aggregate: :average,
    title: "Precipitation (inches)",
    axis: [title_color: "#85A9C5"]
  )
])
|> Vl.resolve(:scale, y: :independent)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/weather.csv"},"encoding":{"x":{"axis":{"format":"%b","title":null},"field":"date","timeUnit":"month"}},"height":300,"layer":[{"encoding":{"y":{"aggregate":"average","axis":{"titleColor":"#85C5A6"},"field":"temp_max","scale":{"domain":[0,30]},"title":"Avg. Temperature (°C)"},"y2":{"aggregate":"average","field":"temp_min"}},"mark":{"color":"#85C5A6","opacity":0.3,"type":"area"}},{"encoding":{"y":{"aggregate":"average","axis":{"titleColor":"#85A9C5"},"field":"precipitation","title":"Precipitation (inches)"}},"mark":{"interpolate":"monotone","stroke":"#85A9C5","type":"line"}}],"resolve":{"scale":{"y":"independent"}},"transform":[{"filter":"datum.location == 'Seattle'"}],"width":400}
```

<!-- livebook:{"branch_parent_index":0} -->

## Concatenation

### Arbitrary charts

You can concatenate arbitrary charts, but it's most useful if they concern the same data.

```elixir
# Source: https://vega.github.io/vega-lite/examples/vconcat_weather.html

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/weather.csv")
|> Vl.transform(filter: "datum.location == 'Seattle'")
|> Vl.concat(
  [
    Vl.new()
    |> Vl.mark(:bar)
    |> Vl.encode_field(:x, "date", time_unit: :month, type: :ordinal)
    |> Vl.encode_field(:y, "precipitation", aggregate: :mean),
    Vl.new()
    |> Vl.mark(:point)
    |> Vl.encode_field(:x, "temp_min", bin: true)
    |> Vl.encode_field(:y, "temp_max", bin: true)
    |> Vl.encode(:size, aggregate: :count)
  ],
  :vertical
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/weather.csv"},"transform":[{"filter":"datum.location == 'Seattle'"}],"vconcat":[{"encoding":{"x":{"field":"date","timeUnit":"month","type":"ordinal"},"y":{"aggregate":"mean","field":"precipitation"}},"mark":"bar"},{"encoding":{"size":{"aggregate":"count"},"x":{"bin":true,"field":"temp_min"},"y":{"bin":true,"field":"temp_max"}},"mark":"point"}]}
```

<!-- livebook:{"branch_parent_index":0} -->

## Maps (geographic displays)

### Projection

A cartographic projection allows for mapping longitude and latitude
pairs to x, y coordinates.

```elixir
# Source: https://vega.github.io/vega-lite/docs/projection.html

Vl.new(width: 500, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/airports.csv")
|> Vl.projection(type: :albers_usa)
|> Vl.mark(:circle)
|> Vl.encode_field(:longitude, "longitude", type: :quantitative)
|> Vl.encode_field(:latitude, "latitude", type: :quantitative)
|> Vl.encode(:size, value: 10)
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"url":"https://vega.github.io/editor/data/airports.csv"},"encoding":{"latitude":{"field":"latitude","type":"quantitative"},"longitude":{"field":"longitude","type":"quantitative"},"size":{"value":10}},"height":300,"mark":"circle","projection":{"type":"albersUsa"},"width":500}
```

### Choropleth map

A Choropleth map is a map composed of colored polygons,
used to represent spatial variations of a quantity.

```elixir
# Source: https://vega.github.io/vega-lite/examples/geo_choropleth.html

Vl.new(width: 500, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/us-10m.json",
  format: [type: :topojson, feature: "counties"]
)
|> Vl.transform(
  lookup: "id",
  from: [
    data: [url: "https://vega.github.io/editor/data/unemployment.tsv"],
    key: "id",
    fields: ["rate"]
  ]
)
|> Vl.projection(type: :albers_usa)
|> Vl.mark(:geoshape)
|> Vl.encode_field(:color, "rate", type: :quantitative)
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"data":{"format":{"feature":"counties","type":"topojson"},"url":"https://vega.github.io/editor/data/us-10m.json"},"encoding":{"color":{"field":"rate","type":"quantitative"}},"height":300,"mark":"geoshape","projection":{"type":"albersUsa"},"transform":[{"from":{"data":{"url":"https://vega.github.io/editor/data/unemployment.tsv"},"fields":["rate"],"key":"id"},"lookup":"id"}],"width":500}
```

<!-- livebook:{"branch_parent_index":0} -->

## Interactive graphics

### Overview and detail

Two charts - one for selecting the range of interest, and the other
for displaying that specific range.

```elixir
# Source: https://vega.github.io/vega-lite/examples/interactive_overview_detail.html

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/sp500.csv")
|> Vl.concat(
  [
    Vl.new(width: 480)
    |> Vl.mark(:area)
    |> Vl.encode_field(:x, "date",
      type: :temporal,
      scale: [domain: [param: "brush"]],
      axis: [title: nil]
    )
    |> Vl.encode_field(:y, "price", type: :quantitative),
    Vl.new(width: 480, height: 60)
    |> Vl.param("brush", select: [type: :interval, encodings: [:x]])
    |> Vl.mark(:area)
    |> Vl.encode_field(:x, "date", type: :temporal)
    |> Vl.encode_field(:y, "price", type: :quantitative, axis: [tick_count: 3, grid: false])
  ],
  :vertical
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/sp500.csv"},"vconcat":[{"encoding":{"x":{"axis":{"title":null},"field":"date","scale":{"domain":{"param":"brush"}},"type":"temporal"},"y":{"field":"price","type":"quantitative"}},"mark":"area","width":480},{"encoding":{"x":{"field":"date","type":"temporal"},"y":{"axis":{"grid":false,"tickCount":3},"field":"price","type":"quantitative"}},"height":60,"mark":"area","params":[{"name":"brush","select":{"encodings":["x"],"type":"interval"}}],"width":480}]}
```

### Scatterplot with external links and tooltips

A scatterplot with each point having a tooltip and linking to some page.

```elixir
# Source: https://vega.github.io/vega-lite/examples/point_href.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/cars.json")
|> Vl.transform(
  calculate: "'https://www.google.com/search?q=' + datum.Name",
  as: "url"
)
|> Vl.mark(:point)
|> Vl.encode_field(:x, "Horsepower", type: :quantitative)
|> Vl.encode_field(:y, "Miles_per_Gallon", type: :quantitative)
|> Vl.encode_field(:color, "Origin", type: :nominal)
|> Vl.encode_field(:tooltip, "Name", type: :nominal)
|> Vl.encode_field(:href, "url", type: :nominal)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/cars.json"},"encoding":{"color":{"field":"Origin","type":"nominal"},"href":{"field":"url","type":"nominal"},"tooltip":{"field":"Name","type":"nominal"},"x":{"field":"Horsepower","type":"quantitative"},"y":{"field":"Miles_per_Gallon","type":"quantitative"}},"height":300,"mark":"point","transform":[{"as":"url","calculate":"'https://www.google.com/search?q=' + datum.Name"}],"width":400}
```

### Regular brush

Highlighting points by selecting and dragging a rectangular area.

```elixir
# Source: https://vega.github.io/vega-lite/examples/interactive_brush.html

Vl.new(width: 400, height: 300)
|> Vl.data_from_url("https://vega.github.io/editor/data/cars.json")
|> Vl.param("brush", select: :interval)
|> Vl.mark(:point)
|> Vl.encode_field(:x, "Horsepower", type: :quantitative)
|> Vl.encode_field(:y, "Miles_per_Gallon", type: :quantitative)
|> Vl.encode(:color,
  condition: [param: "brush", field: "Cylinders", type: :ordinal],
  value: :gray
)
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/cars.json"},"encoding":{"color":{"condition":{"field":"Cylinders","param":"brush","type":"ordinal"},"value":"gray"},"x":{"field":"Horsepower","type":"quantitative"},"y":{"field":"Miles_per_Gallon","type":"quantitative"}},"height":300,"mark":"point","params":[{"name":"brush","select":"interval"}],"width":400}
```

### Interactive mean

A brush selection parameterizing the range of data points to calculate mean over.

```elixir
# Source: https://vega.github.io/vega-lite/examples/selection_layer_bar_month.html

Vl.new()
|> Vl.data_from_url("https://vega.github.io/editor/data/weather.csv")
|> Vl.transform(filter: "datum.location == 'Seattle'")
|> Vl.layers([
  Vl.new()
  |> Vl.param("brush", select: [type: :interval, encodings: ["x"]])
  |> Vl.mark(:bar)
  |> Vl.encode_field(:x, "date", time_unit: :month, type: :ordinal)
  |> Vl.encode_field(:y, "precipitation", aggregate: :mean)
  |> Vl.encode(:opacity, value: 0.7, condition: [param: "brush", value: 1]),
  Vl.new()
  |> Vl.transform(filter: [param: "brush"])
  |> Vl.mark(:rule)
  |> Vl.encode_field(:y, "precipitation", aggregate: :mean)
  |> Vl.encode(:color, value: :firebrick)
  |> Vl.encode(:size, value: 3)
])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","data":{"url":"https://vega.github.io/editor/data/weather.csv"},"layer":[{"encoding":{"opacity":{"condition":{"param":"brush","value":1},"value":0.7},"x":{"field":"date","timeUnit":"month","type":"ordinal"},"y":{"aggregate":"mean","field":"precipitation"}},"mark":"bar","params":[{"name":"brush","select":{"encodings":["x"],"type":"interval"}}]},{"encoding":{"color":{"value":"firebrick"},"size":{"value":3},"y":{"aggregate":"mean","field":"precipitation"}},"mark":"rule","transform":[{"filter":{"param":"brush"}}]}],"transform":[{"filter":"datum.location == 'Seattle'"}]}
```

### Map connections

An interactive visualization of connections between locations on a map.

```elixir
# Source: https://vega.github.io/vega-lite/examples/airport_connections.html

Vl.new(width: 800, height: 500)
|> Vl.projection(type: :albers_usa)
|> Vl.layers([
  # Map with regions
  Vl.new()
  |> Vl.data_from_url("https://vega.github.io/editor/data/us-10m.json",
    format: [type: :topojson, feature: "states"]
  )
  |> Vl.mark(:geoshape, fill: "#ddd", stroke: "#fff", stroke_width: 1),
  # Connection lines
  Vl.new()
  |> Vl.data_from_url("https://vega.github.io/editor/data/flights-airport.csv")
  |> Vl.mark(:rule, color: "#000", opacity: 0.35)
  |> Vl.transform(filter: [param: "org", empty: false])
  |> Vl.transform(
    lookup: "origin",
    from: [
      data: [url: "https://vega.github.io/editor/data/airports.csv"],
      key: "iata",
      fields: ["latitude", "longitude"]
    ]
  )
  |> Vl.transform(
    lookup: "destination",
    from: [
      data: [url: "https://vega.github.io/editor/data/airports.csv"],
      key: "iata",
      fields: ["latitude", "longitude"]
    ],
    as: ["latitude2", "longitude2"]
  )
  |> Vl.encode_field(:latitude, "latitude")
  |> Vl.encode_field(:longitude, "longitude")
  |> Vl.encode_field(:latitude2, "latitude2")
  |> Vl.encode_field(:longitude2, "longitude2"),
  # Points
  Vl.new()
  |> Vl.data_from_url("https://vega.github.io/editor/data/flights-airport.csv")
  |> Vl.transform(aggregate: [[op: :count, as: "routes"]], groupby: ["origin"])
  |> Vl.transform(
    lookup: "origin",
    from: [
      data: [url: "https://vega.github.io/editor/data/airports.csv"],
      key: "iata",
      fields: ["state", "latitude", "longitude"]
    ]
  )
  |> Vl.transform(filter: "datum.state !== 'PR' && datum.state !== 'VI'")
  |> Vl.param("org", select: [type: :point, on: :mouseover, nearest: true, fields: ["origin"]])
  |> Vl.mark(:circle)
  |> Vl.encode_field(:latitude, "latitude")
  |> Vl.encode_field(:longitude, "longitude")
  |> Vl.encode_field(:size, "routes", type: :quantitative, scale: [max_range: 1000], legend: nil)
  |> Vl.encode_field(:order, "routes", sort: :descending)
])
|> Vl.config(view: [stroke: nil])
```

<!-- livebook:{"output":true} -->

```vega-lite
{"$schema":"https://vega.github.io/schema/vega-lite/v5.json","config":{"view":{"stroke":null}},"height":500,"layer":[{"data":{"format":{"feature":"states","type":"topojson"},"url":"https://vega.github.io/editor/data/us-10m.json"},"mark":{"fill":"#ddd","stroke":"#fff","strokeWidth":1,"type":"geoshape"}},{"data":{"url":"https://vega.github.io/editor/data/flights-airport.csv"},"encoding":{"latitude":{"field":"latitude"},"latitude2":{"field":"latitude2"},"longitude":{"field":"longitude"},"longitude2":{"field":"longitude2"}},"mark":{"color":"#000","opacity":0.35,"type":"rule"},"transform":[{"filter":{"empty":false,"param":"org"}},{"from":{"data":{"url":"https://vega.github.io/editor/data/airports.csv"},"fields":["latitude","longitude"],"key":"iata"},"lookup":"origin"},{"as":["latitude2","longitude2"],"from":{"data":{"url":"https://vega.github.io/editor/data/airports.csv"},"fields":["latitude","longitude"],"key":"iata"},"lookup":"destination"}]},{"data":{"url":"https://vega.github.io/editor/data/flights-airport.csv"},"encoding":{"latitude":{"field":"latitude"},"longitude":{"field":"longitude"},"order":{"field":"routes","sort":"descending"},"size":{"field":"routes","legend":null,"scale":{"maxRange":1000},"type":"quantitative"}},"mark":"circle","params":[{"name":"org","select":{"fields":["origin"],"nearest":true,"on":"mouseover","type":"point"}}],"transform":[{"aggregate":[{"as":"routes","op":"count"}],"groupby":["origin"]},{"from":{"data":{"url":"https://vega.github.io/editor/data/airports.csv"},"fields":["state","latitude","longitude"],"key":"iata"},"lookup":"origin"},{"filter":"datum.state !== 'PR' && datum.state !== 'VI'"}]}],"projection":{"type":"albersUsa"},"width":800}
```
